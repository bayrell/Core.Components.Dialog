/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
 
namespace Runtime.Web.Dialog;

use Runtime.BaseStruct;
use Runtime.Core.RemoteCallAnswer;


struct DialogModel extends BaseStruct
{
	static const string STYLE_PROMT = "promt";
	static const string STYLE_CONFIRM = "confirm";
	static const string STYLE_ALERT = "alert";
	static const string STYLE_CONTENT = "content";
	
	static const string BUTTON_RESULT_OK = "ok";
	static const string BUTTON_RESULT_CANCEL = "cancel";
	static const string BUTTON_RESULT_EXIT = "exit";
	
	public bool is_open = false;
	public bool is_modal = false;
	public string title = "";
	public string text = "";
	public string input = "";
	public string button_result = "";
	public string message = "";
	public string success_message = "";
	public string error_message = "";
	public primitive tag = null;
	
	
	
	/**
	 * Show dialog
	 */
	pure DialogModel show(DialogModel model, Dict params = null)
	{
		if (params == null)
		{
			return model.copy
			{
				"input": "",
				"is_open": true,
				"button_result": "",
				"message": "",
				"success_message": "",
				"error_message": "",
			};
		}
		
		params = params.concat
		{
			"input": params.get("input", ""),
			"title": params.get("title", ""),
			"text": params.get("text", ""),
			"is_open": true,
			"button_result": "",
			"message": "",
			"success_message": "",
			"error_message": "",
		};
		return model.copy(params);
	}
	
	
	
	/**
	 * Hide dialog
	 */
	pure DialogModel hide(DialogModel model, string button_result = "")
	{
		return model.copy
		{
			"is_open": false, "tag": null, "button_result": button_result
		};
	}
	
	
	
	/**
	 * Set input
	 */
	pure DialogModel setInput(DialogModel model, string input)
	{
		return model.copy{ "input": input };
	}
	
	
	
	/**
	 * Set wait message
	 */
	pure DialogModel setWaitMessage(DialogModel model, string message = "") => model.copy
	{
		"message": (message != message) ? message : _("Runtime.Web", "Please wait"),
		"success_message": message,
		"error_message": "",
	};
	
	
	
	/**
	 * Message
	 */
	pure DialogModel setMessage(DialogModel model, string message) => model.copy
	{
		"message": "",
		"success_message": message,
		"error_message": "",
	};
	
	
	
	/**
	 * Set answer
	 */
	pure DialogModel setAnswer(DialogModel model, RemoteCallAnswer answer)
	{
		if (answer.isSuccess()) return static::setSuccess(model, answer.success_message);
		return static::setError(model, answer.error_message);
	}
	
	
	
	/**
	 * Success
	 */
	pure DialogModel setSuccess(DialogModel model, string message) => model.copy
	{
		"message": "",
		"success_message": message,
		"error_message": "",
	};
	
	
	
	/**
	 * Error
	 */
	pure DialogModel setError(DialogModel model, string message) => model.copy
	{
		"message": "",
		"success_message": "",
		"error_message": message,
	};
}